/*
 * Copyright 2018 Globant. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def secureProperties = new Properties()
def securePropertiesFile = file("$rootProject.projectDir/secure.properties").exists() ?
        file("$rootProject.projectDir/secure.properties") : file("$rootProject.projectDir/default.properties")

securePropertiesFile.withInputStream {
    stream -> secureProperties.load(stream)
}

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        applicationId 'com.globant.example'

        versionCode 1
        versionName '1.0'

        minSdkVersion 21
        targetSdkVersion rootProject.ext.sdkVersion

        multiDexEnabled false
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled true
    }

    dexOptions {
        javaMaxHeapSize '2048M'
    }

    lintOptions {
        enable 'StopShip'
    }

    signingConfigs {
        release {
            keyAlias secureProperties['keystore.key_alias']
            keyPassword secureProperties['keystore.key_password']
            storeFile file(secureProperties['keystore.file'])
            storePassword secureProperties['keystore.password']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'io.mockk:mockk:1.8.9'
    testImplementation('androidx.test:rules:1.1.0-beta02') {
        exclude module: 'support-annotations'
    }

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
    androidTestImplementation 'androidx.test:rules:1.1.0-beta02'
    androidTestImplementation('androidx.test:runner:1.1.0-beta02') {
        exclude module: 'support-annotations'
    }
}

task androidTest(dependsOn: 'connectedDebugAndroidTest') {
    group = 'Verification'
    description = 'Runs the tests for debug on connected devices'
}

task unitTest(dependsOn: 'testDebugUnitTest') {
    group = 'Verification'
    description = 'Run unit tests for the debug build'
}

// required for continuous integration
project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}
